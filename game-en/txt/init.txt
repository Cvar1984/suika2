###
### Logo
###

@chapter "Logo"

@bgm stop

@skip disable
@bg white.png 0
@ch c suika.png 1.0
@se suika.ogg
@wait 2.0
@ch c none 1.0
@skip enable

###
### Menu
###

@vol bgm 1.0
@bgm 00.ogg
@bg menu-idle.png 1.0

:MENU
@chapter "Menu"
@gui menu.txt

:START
@se click.ogg
@goto SCENE1

:LOAD
@chapter Load
@goto $LOAD
@goto MENU

:CONFIG
@chapter Config
@gui system.txt cancel
@goto MENU

:QUIT
@goto END

###
### Scene 1 (Mixture of Midori's lines and monologues)
###

:SCENE1

@chapter "SCENE -1-"

@vol bgm 0.0 1.0
@bg white.png 1.0
@bgm cicada.ogg
@vol bgm 1.0 1.0
@bg roof.png 1.0 rule:rule-star.png
@ch c 001-fun.png 1.0
*???*Oh, what a nice breeze...

@vol bgm 0.5 1.0
@ch c none 1.0
*???*(I'm on my school's rooftop, having skipped the remedial class.)

@vol bgm 1.0 1.0
@ch c 001-happy.png 1.0
*???*Hey, it's fine, they don't call the roll.

@vol bgm 0.5 1.0
@ch c none 1.0
*???*(Since this is a country town in Hokkaido, and on the coast, I can see the sea peaking up over the horizon with no buildings or hills to block the view.)

@vol bgm 1.0 1.0
@ch c 001-fun.png 1.0
*???*You're probably wondering why I skipped the remedial class, right?

@vol bgm 0.5 1.0
@ch c none 1.0
*???*(Oh, I forgot to mention, I'm Midori! I'm a high school student and my parents are watermelon farmers!)

@vol bgm 1.0 1.0
@ch c 001-angry.png 1.0
*Midori*It's because...

@vol bgm 0.5 1.0
@ch c none 1.0
*Midori*(This is really important... I can't mess this up!)

@vol bgm 1.0 1.0
@ch c 001-normal.png 1.0
*Midori*I want to... to...

@vol bgm 0.0 1.0
@ch c 001-happy.png 1.0
*Midori*I want to tell you about Suika2!

###
### Scene2 (Greetings on the roof)
###

@chapter "SCENE -2-"

@bgm 02.ogg
@vol bgm 1.0 1.0
@ch c 001-happy.png 1.0
*Midori*Ahem, okay, now that that's out in the open...

+en+*Midori*Hello!
+fr+*Midori*Bonjour!
+de+*Midori*Guten Tag!
+es+*Midori*Buenas Tardes!
+it+*Midori*Buon giorno!
+el+*Midori*Καλημέρα!
+ru+*Midori*Добрый день!
+zh+*Midori*你好！
+tw+*Midori*你好！
+ja+*Midori*こんにちは！

*Midori*Thank you for using Suika2!
*Midori*As you may already know, Suika2 is a visual novel authoring tool or 'engine'.
*Midori*It's free and runs on both Windows and Mac. Awesome, right?

@ch c 001-fun.png 1.0
*Midori*The author told me he wants you to ask questions, do you have any questions for him?
@ch c none 1.0

###
### Scene3 (Rooftop Questions)
###

@chapter "SCENE -3-"

:LOOP1
@choose L1_ADVANTAGE "Why choose Suika2?" L1_ENV "Any system requirements?" L1_NEXT Next

:L1_ADVANTAGE
Suika2 has three main advantages. First of all, portability. We have Windows and Mac apps as well as a Web browser version. All of them work just the same way.
That means game developers don't need to worry about the users' OS!
In addition, game developers can use their favorite OS to develop a game.
Secondly, simplicity. Anyone can start writing scripts with nothing more than a text editor.
Last but not least, freedom. You can use Suika2 in your commercial projects for free.

@set $1 = 1
@ch c 001-fun.png 1.0
*Midori*Oh, wow! I didn't know that. Very cool.
*Midori*Do you have any other questions?
@ch c none 1.0
@goto LOOP1

:L1_ENV
We support most versions of Windows from Windows XP all the way up to Windows 11.
We also support macOS 10.9 and later. (Of course, we also support the latest Ventura release and the M2 Chip.)
When it comes to Web browsers, we support Chrome, Safari, Firefox and Edge.
In addition, Suika2 runs on iPhone, iPad, Android and Linux. (Did you know, Suika2 was initially written on Linux!)
@set $2 = 1
@ch c 001-fun.png 1.0
*Midori*Yes! That's right.
*Midori*The author says he wants you to ask some more questions.
@ch c none 1.0
@goto LOOP1

:L1_NEXT

###
### Scene4 (Coastal Questions)
###

@chapter "SCENE -4-"

@ch c 001-fun.png 1.0
*Midori*Shall we step out and have a walk? Where should we go? ...Well, how about the coast?

@vol bgm 0.0 1.0
@bg coast.png 1.0 c
@vol bgm 1.0 0.0
@bgm coast.ogg
@ch c 001-happy.png 1.0
*Midori*Is there anything else you were wondering?
@ch c none 1.0

:LOOP2
@choose L2_OLD "Is there a 'Suika1'?" L2_CUSTOMIZE "Can I customize it?" L2_NEXT Next

:L2_OLD
Versions before 2.0.0 were named Suika Studio and the author developed it when he was just a high school student.
At 2.0.0, he removed the visual editor and "Studio" naming.
@set $3 = 1
@ch c 001-fun.png 1.0
*Midori*Oh, I see. I didn't know that.
*Midori*The author says he wants you to ask more questions.
@ch c none 1.0
@goto LOOP2

:L2_CUSTOMIZE
You can customize the engine with relative ease; this includes the font, text box and save screen.
If there's anything you want to customize, but can't, please contact the author.
@set $4 = 1
@ch c 001-fun.png 1.0
*Midori*Oh, yes!
*Midori*Do you have any other questions?
@ch c none 1.0
@goto LOOP2

:L2_NEXT

###
### Scene5
### (Last scene on the coast/Mixture of Midori's lines and monologues)
###

@chapter "SCENE -5-"

@ch c 001-fun.png 1.0
*Midori*Thanks for asking so many questions today!

@vol bgm 0.5 1.0
@ch c none 1.0
*Midori*(I just wanted to tell you about Suika2...)

@vol bgm 1.0 1.0
@ch c 001-fun.png 1.0
*Midori*If you have any questions, send me an email at midori@suika2.com anytime!

@vol bgm 0.5 1.0
@ch c none 1.0
*Midori*(Don't hesitate to send me an email. I'll be waiting for your comments.)

@vol bgm 0.0 1.0
@ch c 001-fun.png 1.0
*Midori*See you again!

###
### Scene6 (Added bonus)
###

@if $1 != 1 END
@if $2 != 1 END
@if $3 != 1 END
@if $4 != 1 END

@chapter Bonus

Thank you for selecting all options!
@goto END

###
### Quit Logo
###

:END
@chapter Logo
@skip disable
@vol bgm 0.0 2.0
@bg white.png 1.0
@ch c luxion.png 0.5
@wait 1.0
@ch c none 1.0
