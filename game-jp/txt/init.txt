###
### ロゴ
###

# ※この章は、省略せずに日本語で書かれています。もっと簡潔に書けます。

# 準備を行う
@章 タイトル="ロゴ"
@音楽 停止
@スキップ 不許可

# 白い背景を表示する
@背景 ファイル=white.png

# ロゴを1秒かけて表示する
@キャラ 位置=中央 ファイル=suika.png 秒=1.0

# サウンドを再生して2秒待つ
@効果音 ファイル=suika.ogg
@時間待ち 秒=2.0

# ロゴを1秒かけて消去する
@キャラ 位置=中央 消去 秒=1.0

# 準備を行う
@スキップ 許可

###
### メニュー
###

# ※この章も、省略せずに日本語で書いています。

# BGMを再生する
@音量 トラック=bgm 音量=1.0
@音楽 ファイル=00.ogg

# メニュー画像を1秒かけて表示する
@背景 ファイル=menu-idle.png 秒=1.0

# メニューを表示する
:MENU
@章 タイトル="メニュー"
@メニュー ファイル=menu.txt

# STARTが選択されたとき、ここにジャンプする
:START
@効果音 ファイル=click.ogg
@ジャンプ 行き先=シーン1

# LOADが選択されたとき、ここにジャンプする
:LOAD
@章 タイトル=ロード
@ジャンプ 行き先=$LOAD
@ジャンプ 行き先=MENU

# CONFIGが選択されたとき、ここにジャンプする
:CONFIG
@章 タイトル=コンフィグ
@メニュー ファイル=system.txt キャンセル許可
@ジャンプ 行き先=MENU

# QUITが選択されたとき、ここにジャンプする
:QUIT
@ジャンプ 行き先=END

###
### シーン1 (セリフとモノローグの交錯)
###

# ※この章も、省略せずに日本語で書いています。

:シーン1

# 準備を行う
@章 タイトル="シーン1"
@音量 トラック=bgm 音量=0.0 秒=1.0
@背景 ファイル=white.png 秒=1.0
@音楽 ファイル=cicada.ogg
@音量 トラック=bgm 音量=1.0 秒=1.0
@背景 ファイル=roof.png 秒=1.0 エフェクト=rule:rule-star.png

# 以降、キャラの表示、セリフ、音量の調節を行う

@キャラ 位置=中央 ファイル=001-fun.png 秒=1.0

*？？？*001.ogg*あー、気持ちいい風……。

@音量 トラック=bgm 音量=0.5 秒=1.0
@キャラ 位置=中央 消去 秒=1.0

*？？？*002.ogg*（私は今、夏休みの補習を抜け出して、学校の屋上に出ている。）

@音量 トラック=bgm 音量=1.0 秒=1.0
@キャラ 位置=中央 ファイル=001-happy.png 秒=1.0

*？？？*003.ogg*補習は出欠取らないから、別にいいんだ～。

@音量 トラック=bgm 音量=0.5 秒=1.0
@キャラ 位置=中央 消去 秒=1.0

*？？？*004.ogg*（辺りにビルは見あたらず、少し離れたところには海岸線が見える。ここは北海道の田舎町だ。）

@音量 トラック=bgm 音量=1.0 秒=1.0
@キャラ 位置=中央 ファイル=001-fun.png 秒=1.0

*？？？*005.ogg*どうして補習を抜け出したのかって？

@音量 トラック=bgm 音量=0.5 秒=1.0
@キャラ 位置=中央 消去 秒=1.0

*？？？*006.ogg*（あ、言い忘れてた。私の名前はみどり。この田舎町で女子高生をやってる。家は農家だよ。）

@音量 トラック=bgm 音量=1.0 秒=1.0
@キャラ 位置=中央 ファイル=001-angry.png 秒=1.0

*みどり*007.ogg*それはね……

@音量 トラック=bgm 音量=0.5 秒=1.0
@キャラ 位置=中央 消去 秒=1.0

*みどり*008.ogg*（そうだった。とても大切なことがあったんだ。今日私がここにいる理由は……。）

@音量 トラック=bgm 音量=1.0 秒=1.0
@キャラ 位置=中央 ファイル=001-normal.png 秒=1.0

*みどり*009.ogg*君に……

@音量 トラック=bgm 音量=0.0 秒=1.0
@キャラ 位置=中央 ファイル=001-happy.png 秒=1.0

*みどり*010.ogg*君に……Suika2のことを知ってもらうためだよ！

###
### シーン2 (屋上であいさつする)
###

# ※この章は、省略して英語で記述されています。簡潔です。

@chapter シーン2

@bgm 02.ogg
@vol bgm 1.0 1.0
@ch c 001-happy.png 1.0

*みどり*011.ogg*こんにちは！Suika2を使ってくれてありがと！
*みどり*012.ogg*知ってると思うけど、Suika2は、アドベンチャーゲーム制作ツールだよ。
*みどり*013.ogg*自由に使えて、PCでもMacでも動くなんて、すごくない？

@ch c 001-fun.png 1.0

*みどり*014.ogg*作者が質問してほしいんだって。何か聞きたいことあるかな？

@ch c none 1.0

###
### シーン3 (屋上で質問を受け付ける)
###

# ※この章は、日本語で書いています。英語とどちらがお好みですか？

@章 タイトル=シーン3

:LOOP1
@選択肢 行き先1=L1_ADVANTAGE 選択肢1=何が強みなの？ 行き先2=L1_ENV 選択肢2=動作環境は？ 行き先3=L1_NEXT 選択肢3=次へ

:L1_ADVANTAGE
Suika2の強みは、三つあります。
第一は、移植性。WindowsアプリとMacアプリ、ブラウザ版が用意されており、どれでも同じように動きます。
ゲーム開発者は、ユーザの利用しているOSを気にする必要がありません。
さらに、ゲーム開発者自身の利用しているOSも気にする必要がありません。
第二は、シンプルさ。誰でも簡単にスクリプトを書き始めることが可能です。
第三は、自由さ。商用・非商用を問わず無償で自由に利用可能です。

# 1番のフラグに1をセットする
@フラグをセット $1 = 1

@キャラ 位置=中央 ファイル=001-fun.png 秒=1.0

*みどり*015.ogg*へー、そうだったんだ。私も知らなかったよ。
*みどり*016.ogg*ほかにも聞きたいことある？

@キャラ 位置=中央 消去 秒=1.0

@ジャンプ 行き先=LOOP1

:L1_ENV
Windowsアプリでは、Windows XP以降で動くことを確認しています。最新のWindows 11にも対応しています。
Macアプリでは、Mac OS X 10.9以降で動作することを確認しています。最新のVenturaにも対応しています。
ブラウザ版ではChrome, Safari, Firefox, Edgeに対応しています。
さらにいうと、iPhoneやiPad、Android、Linuxでも動きます。初期にはLinux上で開発されていました。

# 2番のフラグに1をセットする
@フラグをセット $2 = 1

@キャラ 位置=中央 ファイル=001-fun.png 秒=1.0

*みどり*017.ogg*うんうん、そうなんだよね。
*みどり*018.ogg*まだまだ質問してほしいってさ。

@キャラ 位置=中央 消去 秒=1.0

@ジャンプ 行き先=LOOP1

:L1_NEXT

###
### シーン4 (浜で質問を受け付ける)
###

# この章は、再度省略して英語を使っています。

@chapter シーン4

@ch c 001-fun.png 1.0
*みどり*019.ogg*学校出て、ちょっと歩こうか。どこに行く？浜の方に出てみようか。

@vol bgm 0.0 1.0
@bg coast.png 1.0 c
@vol bgm 1.0 0.0
@bgm coast.ogg

@ch c 001-happy.png 1.0
*みどり*020.ogg*ほかにも知りたいこととかある？
@ch c none 1.0

:LOOP2
@choose L2_OLD Suika1もあるの？ L2_CUSTOMIZE カスタマイズできるの？ L2_NEXT 次へ

:L2_OLD
2.0.0より前のバージョンは、西瓜Studioというソフトでした。
2001年から2003年にかけて、作者が高校生の頃に開発・公開されていました。
2.0.0以降は、エディタの機能を削ったため、Studioの名前を外しました。
西瓜Studio Ver.0.47.4が、ベクター様のサイトにまだ残っています。
@set $3 = 1
@ch c 001-fun.png 1.0
*みどり*015.ogg*へー、そうだったんだ。私も知らなかったよ。
*みどり*018.ogg*まだまだ質問してほしいってさ。
@ch c none 1.0
@goto LOOP2

:L2_CUSTOMIZE
フォント、メッセージボックス、セーブ画面などがカスタマイズ可能です。
もし、カスタマイズしたいけどできない、という項目があれば、ご連絡ください。
@set $4 = 1
@ch c 001-fun.png 1.0
*みどり*017.ogg*うんうん、そうなんだよね。
*みどり*016.ogg*ほかにも聞きたいことある？
@ch c none 1.0
@goto LOOP2

:L2_NEXT

###
### シーン5 (浜でラスト・セリフとモノローグの交錯)
###

# この章も、省略して英語を使っています。

@chapter シーン5

@ch c 001-fun.png 1.0
*みどり*021.ogg*今日はいろいろ聞いてくれてありがとうね。

@vol bgm 0.5 1.0
@ch c none 1.0
*みどり*022.ogg*（君に、Suika2のことを知ってもらいたくて……）

@vol bgm 1.0 1.0
@ch c 001-fun.png 1.0
*みどり*023.ogg*何かわからないことがあったら、いつでも midori@luxion.jp までメールしてよ。

@vol bgm 0.5 1.0
@ch c none 1.0
*みどり*024.ogg*（……本当にメールしていいんだからね？　メールしてくれるかな？）

@vol bgm 0.0 1.0
@ch c 001-fun.png 1.0
*みどり*025.ogg*それじゃ、また会おうね！

###
### シーン6 (おまけ)
###

# この章は、日本語を使っています。

# 各フラグがセットされていなければENDにジャンプする
@フラグでジャンプ $1 != 1 END
@フラグでジャンプ $2 != 1 END
@フラグでジャンプ $3 != 1 END
@フラグでジャンプ $4 != 1 END

@章 タイトル="おまけ"

全部読んでいただいてありがとうございました。

@goto END

###
### 終了ロゴ
###

# この章は、英語を使っています。

:END
@chapter ロゴ
@skip disable
@vol bgm 0.0 2.0
@bg white.png 1.0
@ch c luxion.png 0.5
@wait 1.0
@ch c none 1.0
